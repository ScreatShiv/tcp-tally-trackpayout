[Report: ALL_Debit_Voucher_Report]
	
	Form: ALL_Debit_Voucher_Form
	
[Form: ALL_Debit_Voucher_Form]
	
	Part: ALL_Debit_Voucher_Part
	

[Part: ALL_Debit_Voucher_Part]
	
	Line	: ALL_Debit_Voucher_Part
	Repeat	: ALL_Debit_Voucher_Part	: CollectionOfAllDebitVoucher
	Scroll	: Vertical
	JSONTag	: "Transaction"

[Line: ALL_Debit_Voucher_Part]
	
	Field: TPO_cmp_id, TPO_guid, TPO_voucher_type, TPO_vouchertype_alias, TPO_parent_type
	Field: TPO_customer, TPO_invoice_date, TPO_invoice_no, TPO_invoice_amount
	Field: TPO_narration, TPO_closingbalance_date, TPO_closing_balance
	JSONTag	: "AllDebitVoucher"

	Add: Option	: TPO DVSubscriptionId	: Not $$IsEmpty:@@TPO_CMPSubscriptionDetail

[!Line: TPO DVSubscriptionId]
	
	Add: Field: Before: TPO_cmp_id	: TPO DVSubscriptionId
	
[Field: TPO DVSubscriptionId]
	
	Use		: Name Field
	Set as	: @@TPO_CMPSubscriptionDetail
	JSONTag	: "subscription_id"
	
[#Field: Vch ref]
	
	Add: Table: CollectionOfAllDebitVoucher

[Collection: CollectionOfAllDebitVoucher]
		
	Source Collection	: CollectionOfAllDebitVoucherSrc
	Walk				: AllLedgerEntries
	
	Compute		: XGUID								: $GUID
	Compute		: XVoucherTypeName					: $VoucherTypeName
	Compute		: XVoucherTypeName_Alias			: $Parent:VoucherType:$VoucherTypeName
	Compute		: XParentType						: "DEBTORS"
	Compute		: XCustomer							: $GUID:Ledger:$LedgerName
	Compute		: XledgerName						: $LedgerName
	Compute		: XDate								: $Date
	Compute		: XInvoiceNo						: $VoucherNumber
	Compute		: XInvoice_amount					: $Amount
	Compute		: XNarration						: "NA"
	Compute		: XClosingBalance					: $ClosingBalance:Ledger:$LedgerName
	Compute		: XDeemedPositive					: $IsDeemedPositive
	Compute		: XVCHALTERID						: $$Owner:$ALTERID
	Keep Source: ()..
	
	Filter		: FilterAllCustomerDebitVoucher, FilterDebitVoucherAlterID
	
	Format	: $XGUID
	Format	: $XDate
	Format	: $XInvoiceNo
	Format	: $XVoucherTypeName
	Format	: $XVoucherTypeName_Alias
	Format	: $XParentType
	Format	: $XledgerName
	Format	: $XInvoice_amount
	Format	: $XDeemedPositive
	Format	: $XVCHALTERID


[System: Formulae]
	
	FilterAllCustomerDebitVoucher	: $XDeemedPositive = "Yes" And  ($$IsObjectBelongsTo:Group:($Parent:Ledger:$XledgerName):$$GroupSundryDebtors) 
	FilterDebitVoucherAlterID :  $$Number:$XVCHALTERID > $$Number:$TPO_Prev_CMPTRDetail:company:##SvCurrentCompany;
	

[Field: TPO_cmp_id]
	
	Use	: Name Field
	Set as	: @@IsTrackPayout_CMPID
	JSONTag	: "company_id"
	
[Field: TPO_guid]
	
	Use	: Name Field
	Set as	: $XGUID
	JSONTag: "guid"
	
[Field: TPO_voucher_type]
	
	Use	: Name Field
	Set as	: $XVoucherTypeName
	JSONTag: "voucher_type"
	
[Field: TPO_vouchertype_alias]
	
	Use	: Name Field
	Set as	: $XVoucherTypeName_Alias
	JSONTag:"vouchertype_alias"
	
[Field: TPO_parent_type]
	
	Use		: Name Field
	Set as	: $XParentType
	JSONTag : "parent_type"
	
[Field: TPO_customer]
	
	Use	: Name Field
	Set as	: $XCustomer
	JSONTag	: "customer"
[Field: TPO_invoice_date]

	Use	: Name Field
	Set as	: $$TPO_DateMMYYYYFormat:$$Date:$XDate
	JSONTag	: "invoice_date"
	
[Field: TPO_invoice_no]

	Use	: Name Field
	Set as	: if $$IsEmpty:$XInvoiceNo then "NA" else $XInvoiceNo
	JSONTag	: "invoice_no"
	
[Field: TPO_invoice_amount]

	Use	: Name Field
	Set as	: $$Number:$XInvoice_amount
	JSONTag	: "invoice_amount"	
	
[Field: TPO_narration]
	
	Use	: Name Field
	Set as	: if $$IsEmpty:$XNarration then "NA" else $XNarration
	JSONTag	: "narration"
	
	
[Field: TPO_closingbalance_date]

	Use	: Name  Field
	Set as	: $$TPO_DateMMYYYYFormat:$$Date:($LASTVOUCHERDATE:Company:$$CurrentCompany)
	JSONTag	: "closingbalance_date"	
	
[Field: TPO_closing_balance]
	
	Use	: Name Field
	Set as	:  if $$IsDr:$XClosingBalance then  $$String:"-" +   $$String:@ConvertInNumber else  $$String:@ConvertInNumber
	ConvertInNumber:$$Number:$XClosingBalance
	JSONTag	: "closing_balance"
	

 
[Collection: CollectionOfAllDebitVoucherSrc]
	
	Type	: Voucher
	Fetch	: ?

	Parm Var	: SVFromDate: Date: $BooksFrom:Company:$$CurrentCompany
	Parm Var	: SVToDate: Date: $LASTVOUCHERDATE:Company:$$CurrentCompany
	
	Filter		: GetFiltertheReport2