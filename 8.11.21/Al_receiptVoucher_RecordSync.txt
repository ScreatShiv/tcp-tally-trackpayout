[Report: TPO_ALL_ReceiptVoucher_Rep]
	
	Form	: TPO_ALL_ReceiptVoucher_Rep

[Form: TPO_ALL_ReceiptVoucher_Rep]
	
	Part: TPO_ALL_ReceiptVoucher_Rep
	
[Part: TPO_ALL_ReceiptVoucher_Rep]
	
	Line	: TPO_ALL_ReceiptVoucher_Rep
	Repeat	: TPO_ALL_ReceiptVoucher_Rep		: COLLECTIONOFCREDITRECEIPTVOUCHERDEBTORSSUMM
	Scroll	: Vertical
	JSONTag	: "Transaction"
	
[Line: TPO_ALL_ReceiptVoucher_Rep]
	
	Field	: TPO RVID, TPO RVreceipt_gu_id
	Field	: TPO RVcustomer_guid, TPO RVinvoice_no
	Field	: TPO RVpaid_amount, TPO RVdeduction_amount
	Field	: TPO RVdescription, TPO RVpayment_date
	JSONTag	: "Receipt"

	Add: Option	: TPO RVSubscriptionId	: Not $$IsEmpty:@@TPO_CMPSubscriptionDetail

[!Line: TPO RVSubscriptionId]
	
	Add: Field: Before: TPO RVID	: TPO RVSubscriptionId
	
[Field: TPO RVSubscriptionId]
	
	Use		: Name Field
	Set as	: @@TPO_CMPSubscriptionDetail
	JSONTag	: "subscription_id"
	
[Collection: CollectionofReceiptVoucher]
	
	Type		: Vouchers: VoucherType
	Child Of	: $$VchTypeReceipt
	Belongs To	: Yes
	Fetch		: GUID,VoucherTypeName,VoucherNumber,Date,PartyLedgerName,Amount,Narration,,IsOptional,IsCancelled, MasterID, AlterID
	Filter		: GetFiltertheReport_receipt
	
	Parm Var	: SVFromDate: Date: $TPO_SyncFromDate:Company:$$CurrentCompany
	Parm Var	: SVToDate: Date: $LASTVOUCHERDATE:Company:$$CurrentCompany

[Collection: COLLECTIONOFCREDITRECEIPTVOUCHERDEBTORSSUMM]
	
		Source Collection: CollectionofReceiptVoucher			
		Walk: ALLLedgerEntries, BillAllocations
		
		compute:  XGUID			: $$Owner:$GUID 
		compute:  XVCHNO		: $$Owner:$$Owner:$VoucherNumber
		compute:  XVCHVTYP		: $$Owner:$VOUCHERTYPENAME
		compute:  XVCHDT		: $$Owner:$$Owner:$Date
		compute:  XLEDGERNAME	: $GUID:Ledger:($$Owner:$LEDGERNAME)
		compute:  XXLEDGERNAME	: $$Owner:$LEDGERNAME
		compute:  XINVOICENO	: $NAME
		compute:  XAMOUNT		: $AMOUNT
		compute:  XDRORCRFLAG	: $IsDeemedPositive
		compute:  XVCHALTERID	: $$Owner:$ALTERID
		compute:  XVCHNarration	: "NA"
		compute:  XPARENTTYPE	: "DEBTORS"
		Keep Source: ()..
		
		Filter: FilterSundryDebtorParty,FilterAlterID
		


[System: Formula]
	
	TPO_AllDebitVoucher			: ($$IsObjectBelongsTo:VoucherType:$VoucherTypeName:$$VchTypeSales) or ($$IsObjectBelongsTo:VoucherType:$VoucherTypeName:$$VchTypeJournal) or ($$IsObjectBelongsTo:VoucherType:$VoucherTypeName:$$VchTypeDebitNote) or ($$IsObjectBelongsTo:VoucherType:$VoucherTypeName:$$VchTypePayment)

	TPO_AllCreditVoucher			: ($$IsObjectBelongsTo:VoucherType:$VoucherTypeName:$$VchTypePurchase) or ($$IsObjectBelongsTo:VoucherType:$VoucherTypeName:$$VchTypeJournal) or ($$IsObjectBelongsTo:VoucherType:$VoucherTypeName:$$VchTypeCreditNote) or ($$IsObjectBelongsTo:VoucherType:$VoucherTypeName:$$VchTypeReceipt)
								
	FilterSundryDebtorParty : ($$IsObjectBelongsTo:Group:($Parent:Ledger:$XXLEDGERNAME):$$GroupSundryDebtors) And $XDRORCRFLAG = "No"
	GetFiltertheReport_receipt:  Not ($IsOptional or $IsCancelled ) And ($Date >= $$Date:##SVFromDate and $Date <= $$Date:##SVToDate)
	
	GetFiltertheReport2: @@TPO_AllDebitVoucher And  Not ($IsOptional or $IsCancelled ) And ($Date >= $$Date:##SVFromDate and $Date <= $$Date:##SVToDate)
	GetFiltertheReport3: @@TPO_AllCreditVoucher And  Not ($IsOptional or $IsCancelled ) And ($Date >= $$Date:##SVFromDate and $Date <= $$Date:##SVToDate)
	FilterAlterID :  $$Number:$XVCHALTERID > $$Number:$TPO_Prev_CMPTRDetail:company:##SvCurrentCompany; AND $$Number:$XVCHALTERID <= $$Number:$TPO_Curr_CMPTRDetail:company:##SvCurrentCompany

[Field: TPO RVID]
	
	Use	: Name Field
	Set as	: @@IsTrackPayout_CMPID
	JSONTag	: "company_id"
[Field: TPO RVreceipt_gu_id]

	Use	: Name Field
	Set as	: $XGUID
	JSONTag	: "receipt_gu_id"	

[Field: TPO RVcustomer_guid]
	
	Use	: Name Field
	Set as	: $XLEDGERNAME
	JSONTag	: "customer_guid"
[Field: TPO RVinvoice_no]
	
	Use	: Name Field
	Set as	: $XINVOICENO
	JSONTag	: "invoice_no"
[Field: TPO RVpaid_amount]
	
	Use	: Name Field
	Set as	: $$Number:$XAMOUNT
	JSONTag	: "paid_amount"
[Field: TPO RVdeduction_amount]
	
	Use	: Name Field
	Set as	: "0"
	JSONTag	: "deduction_amount"
[Field: TPO RVdescription]
	
	Use	: Name Field
	Set as	: if $$IsEmpty:$XVCHNarration then "NA" else $XVCHNarration
	JSONTag	: "description"
	
[Field: TPO RVpayment_date]
	
	Use	: Name Field
	Set as	: $$TPO_DateMMYYYYFormat:$$Date:$XVCHDT
	JSONTag	: "payment_date"
	