[Report: ALL_Credit_Voucher_Report]
	
	Form: ALL_Credit_Voucher_Form
	
[Form: ALL_Credit_Voucher_Form]
	
	Part: ALL_Credit_Voucher_Part
	

[Part: ALL_Credit_Voucher_Part]
	
	Line: ALL_Credit_Voucher_Part
	Repeat: ALL_Credit_Voucher_Part	: CollectionOfAllCreditVoucher
	Scroll	: Vertical
	JSONTag	: "Transaction"

[Line: ALL_Credit_Voucher_Part]
	
	Field: TPO_CR_cmp_id, TPO_CR_guid, TPO_CR_voucher_type, TPO_CR_vouchertype_alias, TPO_CR_parent_type
	Field: TPO_CR_customer, TPO_CR_invoice_date, TPO_CR_invoice_no, TPO_CR_invoice_amount
	Field: TPO_CR_narration, TPO_CR_closingbalance_date, TPO_CR_closing_balance
	JSONTag	: "AllCreditVoucher"
 
	Add: Option	: TPO CVSubscriptionId	: Not $$IsEmpty:@@TPO_CMPSubscriptionDetail

[!Line: TPO CVSubscriptionId]
	
	Add: Field: Before: TPO_CR_cmp_id	: TPO CVSubscriptionId
	
[Field: TPO CVSubscriptionId]
	
	Use		: Name Field
	Set as	: @@TPO_CMPSubscriptionDetail
	JSONTag	: "subscription_id"
	

[Collection: CollectionOfAllCreditVoucher]
		
	Source Collection	: CollectionOfAllCreditVoucherSrc
	Walk				: AllLedgerEntries
	
	Compute		: XGUID								: $GUID
	Compute		: XVoucherTypeName					: $VoucherTypeName
	Compute		: XVoucherTypeName_Alias			: if ($$IsObjectBelongsTo:VoucherType:$VoucherTypeName:$$VchTypePurchase) then "Purchase" else + 
														if ($$IsObjectBelongsTo:VoucherType:$VoucherTypeName:$$VchTypeJournal) then "Journal" else+
														if ($$IsObjectBelongsTo:VoucherType:$VoucherTypeName:$$VchTypeCreditNote) then "Credit Note" else +
														if ($$IsObjectBelongsTo:VoucherType:$VoucherTypeName:$$VchTypeReceipt)  then "Receipt" else ""
	Compute		: XParentType						: "DEBTORS"
	Compute		: XCustomer							: $GUID:Ledger:$LedgerName
	Compute		: XledgerName						: $LedgerName
	Compute		: XDate								: $Date
	Compute		: XInvoiceNo						: $VoucherNumber
	Compute		: XInvoice_amount					: $Amount
	Compute		: XNarration						: "NA"
	Compute		: XClosingBalance					: $ClosingBalance:Ledger:$LedgerName
	Compute		: XDeemedPositive					: $IsDeemedPositive
	Compute		: XVCHALTERID						: $$Owner:$ALTERID
	Keep Source: ()..
	
	Filter		: FilterAllCustomerCreditVoucher, FilterCreditVoucherAlterID
	
	Format	: $XGUID
	Format	: $XDate
	Format	: $XInvoiceNo
	Format	: $XVoucherTypeName
	Format	: $XVoucherTypeName_Alias
	Format	: $XParentType
	Format	: $XledgerName
	Format	: $XInvoice_amount
	Format	: $XDeemedPositive


[System: Formulae]
	
	FilterAllCustomerCreditVoucher	: $XDeemedPositive = "No" And  ($$IsObjectBelongsTo:Group:($Parent:Ledger:$XledgerName):$$GroupSundryDebtors) 
	FilterCreditVoucherAlterID :  $$Number:$XVCHALTERID > $$Number:$TPO_Prev_CMPTRDetail:company:##SvCurrentCompany;
	

[Field: TPO_CR_cmp_id]
	
	Use	: Name Field
	Set as	: @@IsTrackPayout_CMPID
	JSONTag	: "company_id"
	
[Field: TPO_CR_guid]
	
	Use	: Name Field
	Set as	: $XGUID
	JSONTag: "guid"
	
[Field: TPO_CR_voucher_type]
	
	Use	: Name Field
	Set as	: $XVoucherTypeName
	JSONTag: "voucher_type"
	
[Field: TPO_CR_vouchertype_alias]
	
	Use	: Name Field
	Set as	: $XVoucherTypeName_Alias
	JSONTag:"vouchertype_alias"
	
[Field: TPO_CR_parent_type]
	
	Use		: Name Field
	Set as	: $XParentType
	JSONTag : "parent_type"
	
[Field: TPO_CR_customer]
	
	Use	: Name Field
	Set as	: $XCustomer
	JSONTag	: "customer"
[Field: TPO_CR_invoice_date]

	Use	: Name Field
	Set as	: $$TPO_DateMMYYYYFormat:$$Date:$XDate
	JSONTag	: "invoice_date"
	
[Field: TPO_CR_invoice_no]

	Use	: Name Field
	Set as	: if $$IsEmpty:$XInvoiceNo then "NA" else $XInvoiceNo
	JSONTag	: "invoice_no"
	
[Field: TPO_CR_invoice_amount]

	Use	: Name Field
	Set as	: $$Number:$XInvoice_amount
	JSONTag	: "invoice_amount"	
	
[Field: TPO_CR_narration]
	
	Use	: Name Field
	Set as	: if $$IsEmpty:$XNarration then "NA" else $XNarration
	JSONTag	: "narration"
	
	
[Field: TPO_CR_closingbalance_date]

	Use	: Name  Field
	Set as	: $$TPO_DateMMYYYYFormat:$$Date:($LASTVOUCHERDATE:Company:$$CurrentCompany)
	JSONTag	: "closingbalance_date"	
	
[Field: TPO_CR_closing_balance]
	
	Use		: Name Field
	Set as	:  if $$IsDr:$XClosingBalance then  $$String:"-" +   $$String:@ConvertInNumber else  $$String:@ConvertInNumber
	ConvertInNumber:$$Number:$XClosingBalance
	JSONTag	: "closing_balance"
	

 
[Collection: CollectionOfAllCreditVoucherSrc]
	
	Type	: Voucher
	Fetch	: ?

	Parm Var	: SVFromDate: Date: $TPO_SyncFromDate:Company:$$CurrentCompany
	Parm Var	: SVToDate: Date: $LASTVOUCHERDATE:Company:$$CurrentCompany
	
	Filter		: GetFiltertheReport3