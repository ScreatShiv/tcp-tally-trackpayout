[Report: TPO_ALL_SalesVoucher_Rep]
	
	Form	: TPO_ALL_SalesVoucher_Rep
	
[Form	: TPO_ALL_SalesVoucher_Rep]
	
	Part	: TPO_ALL_SalesVoucher_Rep
	

[Part: TPO_ALL_SalesVoucher_Rep]
	
	Line	: TPO_ALL_SalesVoucher_Rep	
	Repeat	: TPO_ALL_SalesVoucher_Rep	: CollectionofSalesVoucher
	Scroll	: Vertical
	JSONTag	: "Transaction"

[Collection: CollectionofSalesVoucher]
	
	Parm Var	: SVFromDate: Date: $TPO_SyncFromDate:Company:$$CurrentCompany
	Parm Var	: SVToDate: Date: $LASTVOUCHERDATE:Company:$$CurrentCompany
	
	Type		: Vouchers: VoucherType
	Child Of	: $$VchTypeSales
	Belongs To	: Yes
	Fetch		: GUID,Reference,BasicDueDateOfPymt,VoucherTypeName,VoucherNumber,Date,PartyLedgerName,Amount,Narration,InventoryEntries.*,InventoryEntries.Amount,LedgerEntries.*, IsCancelled, IsOptional
	Fetch		: LedgerEntries.BillAllocations.*,InvoiceOrderList.*,alterid
	
	Compute	: billReferenceNo: if $IsBillWiseOn:Ledger:$PatyLedgerName = "Yes" then (if $$IsEmpty:$LedgerEntries.BillAllocations.Name then "NA" else $LedgerEntries.BillAllocations.Name) else $VoucherNumber
	Compute	: customerId: $GUID:Ledger:$PartyLedgerName
	Compute: IGSTAmt   : if $$IsEmpty:$$FilterAmtTotal:LedgerEntries:IsTPOIGSTLedger:$Amount then "0" else  $$FilterAmtTotal:LedgerEntries:IsTPOIGSTLedger:$Amount
	Compute: SGSTAmt   : if $$IsEmpty:$$FilterAmtTotal:LedgerEntries:IsTPOSGSTLedger:$Amount then "0" else  $$FilterAmtTotal:LedgerEntries:IsTPOSGSTLedger:$Amount
	Compute: CGSTAmt   : if $$IsEmpty:$$FilterAmtTotal:LedgerEntries:IsTPOCGSTLedger:$Amount then "0" else  $$FilterAmtTotal:LedgerEntries:IsTPOCGSTLedger:$Amount
	Compute: TAXABLEAmt   :  if $$IsEmpty:$Amount then "0" else $$Number:$Amount
	Compute: PONumber  :  if $$IsEmpty:$$CollectionField:$BasicPurchaseOrderNo:1:InvoiceOrderList then "NA" else $$CollectionField:$BasicPurchaseOrderNo:1:InvoiceOrderList
	Compute: PODate  :  if $$IsEmpty:$$CollectionField:$BasicOrderDate:1:InvoiceOrderList then "null" else $$CollectionField:$BasicOrderDate:1:InvoiceOrderList
	Compute: Paymentterm  : if $$IsEmpty:$BasicDueDateOfPymt then "NA" else $$Number:$BasicDueDateOfPymt
	Compute: Description  :  "NA"
	Compute: PaymentDueDate  : if $$IsEmpty:$LedgerEntries.BillAllocations.BillCreditPeriod then $Date + $$Number:30 else (if $$String:$LedgerEntries.BillAllocations.BillCreditPeriod contains 'Days' then $Date + $$Number:$LedgerEntries.BillAllocations.BillCreditPeriod else $$Date:($$String:($LedgerEntries.BillAllocations.BillCreditPeriod):'Short Date'))
	Filter	: GetFiltertheReport
	Keep Source: ()..

	
	
[System: Formula]
	
	GetFiltertheReport: Not ($IsOptional or $IsCancelled) + 
							And ($$IsObjectBelongsTo:Group:($Parent:Ledger:$PartyLedgerName):$$GroupSundryDebtors) And $Date >= $$Date:##SVFromDate and $Date  <= $$Date:##SVToDate +
							And $$Number:$alterid > $$Number:$TPO_Prev_CMPTRDetail:company:##SvCurrentCompany ;AND $$Number:$alterid <= $$Number:$TPO_Curr_CMPTRDetail:company:##SvCurrentCompany
	IsTPOIGSTLedger: $GSTDutyHead:Ledger:$LedgerName = "Integrated Tax"
	IsTPOSGSTLedger: $GSTDutyHead:Ledger:$LedgerName = "State Tax"
	IsTPOCGSTLedger: $GSTDutyHead:Ledger:$LedgerName = "Central Tax"
	

[Line: TPO_ALL_SalesVoucher_Rep]
	
	Field	: TPO SVID, TPO SVInvoiceGuid, TPO SVCustomerGuid, TPO SVcate
	Field	: TPO SVPONO, TPO SVPODate, TPO SVInvNo, TPO SVBillRefNo, TPO SVInvDate
	Field	: TPO SVInvAmt, TPO SVigst, TPO SVcgst, TPO SVsgst, TPO SVDesc, TPO SVpymtTerm
	Field	: TPO SVpymtDueDate, TPO SVstartrenminer, TPO SVreminderFreq, TPO SVpymtStatus, TPO SVpymtType
	Field	: TPO SVpymtDate, TPO SVmore_recpt
	
	JSONTag	: "Sales"
	
	Add: Option	: TPO SVSubscriptionId	: Not $$IsEmpty:@@TPO_CMPSubscriptionDetail
	
	Explode	: Sale_Bill_InventoryAllocation: Yes
	Explode	: Sale_Bill_LedgerAllocation: Yes
 
[!Line: TPO SVSubscriptionId]
	
	Add: Field: Before: TPO SVID	: TPO SVSubscriptionId
	
[Field: TPO SVSubscriptionId]
	
	Use		: Name Field
	Set as	: @@TPO_CMPSubscriptionDetail
	JSONTag	: "subscription_id"
	
[Field: TPO SVID]
	
	Use	: Name Field
	Set as	: @@IsTrackPayout_CMPID
	JSONTag	: "company_id"
	
[Field: TPO SVInvoiceGuid]
	
	Use	: Name Field
	Set as	: $GUID
	JSONTag	: "invoice_gu_id"

[Field: TPO SVCustomerGuid]

	Use	: Name Field
	Set as	: $customerId
	JSONTag	: "customer_guid"

		
[Field: TPO SVcate]
	
Use	: Name Field
	Set as	: "NA"
	JSONTag	: "category_id"
	
[Field: TPO SVPONO]
	
Use	: Name Field
	Set as	: if $$IsEmpty:$PONumber then "NA" else $PONumber
	JSONTag	: "po_no"
	
[Field: TPO SVPODate]
	
	
Use	: Name Field
	Set as	: if $PODate = "null" then $$TPO_DateMMYYYYFormat:$$Date:$$CurrentDate else $$TPO_DateMMYYYYFormat:$$Date:$PODate
	JSONTag	: "po_date"
	
	
[Field: TPO SVInvNo]
	
	Use	: Name Field
	Set as	: if $$IsEmpty:$voucherNumber then "NA" else $voucherNumber
	JSONTag	: "invoice_no"

[Function: TPO_DateMMYYYYFormat]

	Parameter   : ParamDate     : Date
	Variable	: DDFormat		: String
	variable    : MONName       : String
	Variable    : YYYYFormat    : String
	Variable    : RetDate       : String

	Returns     : String

	01          : Do If : ($$IsEmpty:##ParamDate)   : Return : ""
	03          : Set           : MONName       : $$Zerofill:($$MonthOfDate:##ParamDate):2
	04          : Set           : YYYYFormat    : ($$YearofDate:##ParamDate)
	04a          : Set          : DDFormat    	: ($$DayOfDate:##ParamDate)
	05          : Set           : RetDate       : ##YYYYFormat + "-" + ##MONName + "-" + ##DDFormat
	06          : Return        : ##RetDate
	


[Field: TPO SVBillRefNo]
	
	Use	: Name Field
	Set as	: $billReferenceNo
	JSONTag	: "bill_reference_no"


[Field: TPO SVInvDate]
	
	Use	: Name Field
	Set as	: $$TPO_DateMMYYYYFormat:$$Date:$Date
	JSONTag	: "invoice_date"
	

[Field: TPO SVInvAmt]
	
	Use			: Name Field
	Set as		: $$TPO_RemoveCharactersFromString:$TAXABLEAmt:"-"
	JSONTag		: "invoice_amount"
 
[Function: TPO_RemoveCharactersFromString]

    Parameter	: pImputStr  	: String
    Parameter	: pTokenStr 	: String
    Variable	: vOutputStr	: String

	0010		: Do If			: ($$ExactMatch:##pImputStr:##pTokenStr)	: Return	: ""
	0020 		: For Token		: Str : ##pImputStr : ##pTokenStr
    0030 		:   Set			: vOutputStr	: ##vOutputStr + ##Str
    0040 		: End For
    0050 		: Return		: If $$IsEmpty:##vOutputStr Then $$StringWordEx:##pImputStr:##pTokenStr:1 Else ##vOutputStr
	
[Field: TPO SVigst]
	
	Use	: Name Field
	Set as	: $IGSTAmt
	JSONTag	: "igst"
	
[Field: TPO SVcgst]
	

	Use	: Name Field
	Set as	: $CGSTAmt
	JSONTag	: "cgst"
	
[Field: TPO SVsgst]

Use	: Name Field
	Set as	: $SGSTAmt
	JSONTag	: "sgst"	
[Field: TPO SVDesc]
	
	Use	: Name Field
	Set as	: $$TPO_RemoveCharactersFromString:($$String:$Description):"*"
	JSONTag	: "description"
	
[Field: TPO SVpymtTerm]
	
Use	: Name Field
	Set as	: "NA"
	JSONTag	: "payment_term"
[Field: TPO SVpymtDueDate]
	
Use	: Name Field
	Set as	: $$TPO_DateMMYYYYFormat:$$Date:$PaymentDueDate
	JSONTag	: "payment_due_date"
	
[Field: TPO SVstartrenminer]

Use	: Name Field
	Set as	: "0"
	JSONTag	: "start_reminder_from"	
[Field: TPO SVreminderFreq]
	
Use	: Name Field
	Set as	: "0"
	JSONTag	: "reminder_frequency"
[Field: TPO SVpymtStatus]
	
Use	: Name Field
	Set as	: "0"
	JSONTag	: "payment_status"
[Field: TPO SVpymtType]
	
Use	: Name Field
	Set as	: "NA"
	JSONTag	: "payment_type"
[Field: TPO SVpymtDate]
	
Use	: Name Field
	Set as	: "NA"
	JSONTag	: "payment_date"
[Field: TPO SVmore_recpt]
	
Use	: Name Field
	Set as	: "NA"
	JSONTag	: "more_receipents"
	

	
	
[!Part: Sale_Bill_InventoryAllocation]
	
	Line: Sale_Bill_InventoryAllocation
	Repeat	: Sale_Bill_InventoryAllocation	: InventoryEntries
	Scroll	: Vertical
	
	
[Line: Sale_Bill_InventoryAllocation]
	
	Field: TPO SVitemName, TPO SVActualQty, TPO SVBilledQty, TPO SVRate, TPO SVAmt, TPO SVIsDrOrCrAmt
	JSONTag: "inventory_detail"

[Field: TPO SVitemName]
		
	Use	: Name Field
	Set as	: $$String:$StockitemName
	JSONTag	: "itemName"
	
[Field: TPO SVActualQty]
	
	Use	: Name Field
	Set as	: $$Number:$ActualQty
	JSONTag: "actual_qty"
	
[Field:  TPO SVBilledQty]
	
	Use	: Name Field
	Set as	: $$Number:$BilledQty
	JSONTag: "billed_qty"
	
[Field: TPO SVRate]
	
	Use	: Name Field
	Set as	: $$Number:$Rate
	Format	: "Decimal:2"
	JSONTag	: "rate"
	
[Field	: TPO SVAmt]
	
	Use		: Name Field
	Set as	: $$TPO_RemoveCharactersFromString:$TAXABLEAmt:"-"
	JSONTag	: "item_amount"
	
[Field: TPO SVIsDrOrCrAmt]
	
	Use	: Name Field
	Set as	: $IsDeemedPositive
	JSONTag	: "item_deemedpositive"
	
[!Part: Sale_Bill_LedgerAllocation]
	
	Line	: Sale_Bill_LedgerAllocation
	Repeat	: Sale_Bill_LedgerAllocation	: ledgerEntries
	Scroll	: Vertical
	
	
[Line: Sale_Bill_LedgerAllocation]
	
	Field	: TPO SVledName, TPO SVledAmt, TPO SVledIsDrOrCrAmt
	JSONTag	: "ledger_detail"
	Explode	: Sale_Bill_Allocation: Yes
[Field: TPO SVledName]
	
	Use	: Name Field
	Set as	: $LedgerName
	JSONTag	: "ledger_name"
	
[Field:  TPO SVledAmt]
	
	Use			: Name Field
	Set as		: $$Number:$$TPO_RemoveCharactersFromString:$amount:"-"
	JSONTag		: "ledger_amount"
	

[Field:TPO SVledIsDrOrCrAmt]
	
	Use		: Name FIeld
	Set as	: $IsDeemedPositive
	JSONTag	: "ledger_deemedpositive"

[!Part: Sale_Bill_Allocation]
	
	Line: Sale_Bill_Allocation
	Repeat	: Sale_Bill_Allocation	: BillAllocations
	Scroll	: Vertical
	
[Line: Sale_Bill_Allocation]
	
	Field: TPO vchBilltype, TPO vchBillNo, TPO vchBillDueDt, TPO vchBillAmt, TPO vchDrOrCrBillAmt

JSONTag	: "vch_bill_detail"
	
[Field: TPO vchBilltype]
	
	Use	: Name Field
	Set as	: $$String:$BillType
	JSONTag: "vch_bill_type"
	

[Field: TPO vchBillNo]
	
	Use	: Name Field
	Set as	: $Name
	JSONTag: "vch_bill_no"
	
[Field: TPO vchBillDueDt]

	Use	: Name Field
	Set as	: if $$IsEmpty:$BillCreditPeriod then $BillDate + $$Number:30 else (if $$String:$BillCreditPeriod contains 'Days' then $BillDate + $$Number:$BillCreditPeriod else $$Date:($$String:($BillCreditPeriod):'Short Date'))
	JSONTag	: "vch_bill_due_date"
	
[Field: TPO vchBillAmt]
	
	Use	: Name Field
	Set as: if $$IsDr:$Amount then $$TPO_RemoveCharactersFromString:$Amount:"-" else $$Number:$Amount
	JSONTag: "vch_bill_amount"
	
[Field: TPO vchDrOrCrBillAmt]
	
	Use	: Name Field
	Set as	: $IsDeemedPositive
	JSONTag: "vch_bill_deemedpositive"
	

	
